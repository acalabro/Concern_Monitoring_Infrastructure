//created on: Nov 11, 2021
package it.cnr.isti.labsedc.concern.event;
import it.cnr.isti.labsedc.concern.event.ConcernAbstractEvent;
import it.cnr.isti.labsedc.concern.event.ConcernProbeEvent;
import it.cnr.isti.labsedc.concern.utils.KieLauncher;
import it.cnr.isti.labsedc.concern.utils.Calculator;

//Communication delay between LocalPlanner and GlobalPlanner exceed 20ms

dialect "java"

declare ConcernBaseEvent
    @role( event )
    @timestamp( timestamp )
end

declare LatencyEvent
    @role( event )
	latency : float
	first : ConcernBaseEvent
	second: ConcernBaseEvent
end
            
	rule "localGlobalAvgDelayCheck"
	no-loop
	salience 10
	dialect "java"
	when
	
		$aEvent: ConcernBaseEvent(this.getSenderID == "LocalPlanner", 
									this.getDestinationID == "GlobalPlanner");
									
		$bEvent: ConcernBaseEvent(this.getSenderID == "GlobalPlanner",
									this.getSessionID == $aEvent.getSessionID,
									this.getData == $aEvent.getData,
									this.getName == $aEvent.getName,
									this.getChecksum == $aEvent.getChecksum,
									this after $aEvent);
	then					
		LatencyEvent warning = new LatencyEvent();
		warning.setLatency(
			Calculator.latency(
				$bEvent.getTimestamp(),
                $aEvent.getTimestamp()));
		KieLauncher.printer("latencyEventCreated " + warning.getLatency());
		
		insert(warning);	                    
		retract($aEvent);
    	retract($bEvent);	
	end
	
	rule "calculateAverage"
	no-loop
	salience 5
	dialect "java"
	when

	    Number( longValue > 20 ) from accumulate(
	        LatencyEvent( $latency : latency ) over window:size( 10 ),
	        average( $latency ) )
	then
		KieLauncher.printer("Boundary violated");
	end
